rule download_reference_from_refseq:
    conda:
        "../../envs/entrez-direct.yml"
    container:
        singularity_envs["python_r"]
    output:
        genome="references/{ref,[0-9]+}/genome_{format}.{format}",
    shell:
        """
        ftp=$(curl -L "http://eutils.ncbi.nlm.nih.gov/entrez/eutils/esummary.fcgi?db=assembly&id={wildcards.ref}" | grep FtpPath_RefSeq | sed 's/.*>\(ftp.*\)<.*/\\1/')
        echo $ftp
        ftp_path=$(echo ${{ftp}} | sed  "s/\(\/GCF_.*\)/\\1\\1_genomic.{wildcards.format}.gz/")
        echo $ftp_path
        wget -O - $ftp_path | gunzip -c > {output[genome]}
        """


rule download_reference_from_nucleotide:
    conda:
        "../../envs/entrez-direct.yml"
    container:
        singularity_envs["entrez-direct"]
    output:
        genome="references/NC{ref}/genome_{format}.{format}",
    shell:
        """
        efetch -db nucleotide -id NC{wildcards.ref} -format {wildcards.format} > {output[genome]} 
        """



rule get_strain_subvalue_identifier_reference:
    conda:
        "../../envs/entrez-direct.yml"
    container:
        singularity_envs["entrez-direct"]
    output:
        subvalue="references/{ref,[0-9]+}/genome_subvalue.txt",
    shell:
        """
        efetch -db assembly -id {wildcards.ref} -format docsum | xtract -pattern DocumentSummary -unless LatestAccession -if AssemblyStatus -equals "Complete Genome" -if FtpPath_RefSeq -element Sub_value > {output[subvalue]}
        """

    
rule download_gff_for_reference_from_refseq:
    conda:
        "../../envs/entrez-direct.yml"
    container:
        singularity_envs["entrez-direct"]
    output:
        gff="references/{ref,[0-9]+}/genome.gff"
    shell:
        """
        ftp=$(efetch -db assembly -id {wildcards.ref} -format docsum | xtract -pattern DocumentSummary -unless LatestAccession -if AssemblyStatus -equals "Complete Genome" -if FtpPath_RefSeq -element FtpPath_RefSeq)
        nucl_id=$(echo ${{ftp}} | sed  "s/\(\/GCF_.*\)/\\1\\1_genomic.gff.gz/")
        wget -qO- ${{nucl_id}} | gzip -d > {output[gff]}
        """

    
