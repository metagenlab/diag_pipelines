rule extract_cgMLST_regions_from_vcf:
    conda:
        pipeline_path + "envs/bcftools.yml"
    singularity:
        "docker://quay.io/biocontainers/bcftools:1.9--h68d8f2e_7"  
    input:
        vcf_mutation_type = "typing/{snp_caller}/full_genome_" + str(all_core_genome_dbs.loc[species, "ReferenceGenome"]) + "/{mapping_method}/all_samples_{type}.vcf.gz",
        vcf_mutation_type_tbi = "typing/{snp_caller}/full_genome_" + str(all_core_genome_dbs.loc[species, "ReferenceGenome"]) + "/{mapping_method}/all_samples_{type}.vcf.gz.tbi",
        bed = "core_genomes/cgMLST/" + species + ".bed",
    output:
        core_genome_vcf = "typing/{snp_caller}/cgMLST/{mapping_method}/all_samples_{type}.vcf",
    shell:
        """
        bcftools view --regions-file {input[bed]} --output-file {output[core_genome_vcf]} {input[vcf_mutation_type]}
        """


rule extract_individual_cgMLST_regions_from_vcf:
    conda:
        pipeline_path + "envs/bcftools.yml"
    singularity:
        "docker://quay.io/biocontainers/bcftools:1.9--h68d8f2e_7"  
    input:
        vcf = "samples/{sample}/snps/{snp_caller}/" + str(all_core_genome_dbs.loc[species, "ReferenceGenome"]) + "/{mapping_method}/freq.vcf.gz",
        vcf_tbi = "samples/{sample}/snps/{snp_caller}/" + str(all_core_genome_dbs.loc[species, "ReferenceGenome"]) + "/{mapping_method}/freq.vcf.gz.tbi",
        bed = "core_genomes/cgMLST/" + species + ".bed",
    output:
        core_genome_vcf = "samples/{sample}/snps/{snp_caller}/cgMLST/{mapping_method}/freq.vcf",
    shell:
        """
        bcftools view --regions-file {input[bed]} --output-file {output[core_genome_vcf]} {input[vcf]}
        """


rule link_reference_genome_for_cgMLST:
    input:
        genome_ref = "references/" + str(all_core_genome_dbs.loc[species, "ReferenceGenome"]) + "/genome_fasta.fasta"
    output:
        genome_cgmlst = "references/cgMLST/genome_{format}.{format}"
    shell:
        """
        ln -sr {input[genome_ref]} {output[genome_cgmlst]}
        """
