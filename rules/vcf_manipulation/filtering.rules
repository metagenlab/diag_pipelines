rule decompose_multiallelics_and_normalize:
    conda:
        "../../envs/vt.yaml"
    input:
        raw="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/raw/all_samples.vcf.gz",
        raw_tbi="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/raw/all_samples.vcf.gz",
        ref="references/{ref}/genome_fasta.fasta",
    output:
        decomposed="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/filtering/decomposed_normalized.vcf",
    log:
        logging_folder+"/typing/{snp_caller}/full_genome/{ref}/{mapping_method}/filtering/decomposed_normalized.txt",
    shell:
        """
        vt decompose -s {input[raw]} | vt normalize -r {input[ref]} -o {output[decomposed]} - &> {log}
        """
        
rule filter_on_coverage:
    params:
        min_cov=minimum_coverage,
    conda:
        "../../envs/bcftools.yaml"
    input:
        decomposed="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/filtering/decomposed_normalized.vcf",
    output:
        vcf_cov="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/filtering/cov_decomposed_normalized.vcf",
    shell:
        """
        bcftools filter -s "lowcov" -S "." -e '(FORMAT/DP)<{params.min_cov}' {input[decomposed]} > {output[vcf_cov]}
        """

rule filter_on_frequency_per_sample:
    params:
        min_alt=minimum_alternate_frac,
    conda:
        "../../envs/bcftools.yaml",
    input:
        sample="samples/{sample}/snps/{snp_caller}/{ref}/{mapping_method}/raw.vcf",
    output:
        freq="samples/{sample}/snps/{snp_caller}/{ref}/{mapping_method}/freq.vcf",
    shell:
        """
        bcftools filter -s "freqalt" -S "." -e "GT='alt' & (FORMAT/AD[0:1])/(FORMAT/DP) < {params.min_alt}" {input[sample]} | bcftools filter -s "freqref" -S "." -e "GT='ref' & (FORMAT/AD[0:0])/(FORMAT/DP) < {params.min_alt}" > {output[freq]}
        """
        
rule extract_allele_by_type_from_gatk_gvcfs:
    conda:
        "../../envs/gatk.yaml"
    input:
        joint_genotype_vcf="typing/gatk_gvcfs/full_genome/{ref}/{mapping_method}/filtering/freq_cov_decomposed_normalized.vcf.gz",
        joint_genotype_vcf_tbi="typing/gatk_gvcfs/full_genome/{ref}/{mapping_method}/filtering/freq_cov_decomposed_normalized.vcf.gz.tbi",
    output:
        vcf_mutation_type="typing/gatk_gvcfs/full_genome/{ref}/{mapping_method}/all_samples_{type}.vcf",
    log:
        logging_folder+"/typing/gatk_gvcfs/full_genome/{ref}/{mapping_method}/all_samples_{type}.txt"
    shell:
        """
        type=$(echo {wildcards.type}  | tr '[:lower:]' '[:upper:]')
        gatk-launch SelectVariants --output {output[vcf_mutation_type]} --variant {input[joint_genotype_vcf]} -select-type ${{type}} &> /dev/null
        """

rule extract_allele_by_type_from_freebayes_joint_genotyping:
    conda:
        "../../envs/bcftools.yaml"
    input:
        joint_genotype_vcf="typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/filtering/freq_cov_decomposed_normalized.vcf.gz",
        joint_genotype_vcf_tbi="typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/filtering/freq_cov_decomposed_normalized.vcf.gz.tbi",
    output:
        vcf_mutation_type="typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/all_samples_{type}.vcf",
    log:
        logging_folder+"/typing/freebayes_joint_genotyping/full_genome/{ref}/{mapping_method}/all_samples_{type}.txt"
    shell:
        """
        bcftools view -i 'type="{wildcards.type}"' {input[joint_genotype_vcf]} > {output[vcf_mutation_type]}
        """

rule extract_core_genome:
    conda:
        "../../envs/bcftools.yaml"
    input:
        vcf_mutation_type="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/all_samples_{type}.vcf.gz",
        vcf_mutation_type_tbi="typing/{snp_caller}/full_genome/{ref}/{mapping_method}/all_samples_{type}.vcf.gz.tbi",
        bed="core_genome/{core_method}/{ref}.bed",
    output:
        core_genome_vcf="typing/{snp_caller}/core_{core_method}/{ref}/{mapping_method}/all_samples_{type}.vcf",
    shell:
        """
        bcftools view --regions-file {input[bed]} --output-file {output[core_genome_vcf]} {input[vcf_mutation_type]}
        """
    

